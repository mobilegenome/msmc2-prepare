
configfile: "config.yaml"

SAMPLES     = config["SAMPLES"]
CHROMOSOMES = config["CHROMOSOMES"]

rule all:
    input:
        expand("results/vcf-merged/{sample}.{chr}.vcf.gz",
        sample=SAMPLES,
        chr=CHROMOSOMES),
        config["REPLACEMENT_STATISTICS_FILENAME"]

if config["DOWNLOAD_VIA_SCP"]:
    rule download_files:
        output:
            unphased=temp("test/vcf-unphased/{sample}.{chr}.vcf.gz"),
            phased=temp("test/vcf-phased/{sample}.{chr}.vcf.gz"),
        shell:
           config["DOWNLOAD_CMD"]

rule merge_genotypes:
    input:
        unphased="test/vcf-unphased/{sample}.{chr}.vcf.gz",
        phased="test/vcf-phased/{sample}.{chr}.vcf.gz",
    output:
        merged_vcf="results/vcf-merged/{sample}.{chr}.vcf.gz",
    log:
        "logs/merge_genotypes/{sample}.{chr}.log"
    shell:
        "python workflow/scripts/merge_genotypes.py {input.unphased} {input.phased} {output.merged_vcf} > {log}"


rule get_stats:
    input:
        logfiles=expand("logs/merge_genotypes/{sample}.{chr}.log", 
            sample=SAMPLES, chr=CHROMOSOMES),
    output:
         tsv=config["REPLACEMENT_STATISTICS_FILENAME"]
    params: 
        fname_suffix="_chr.*"
    script:
        "scripts/aggregate_stats.R"



